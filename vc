 ; ~Format=ANSI.S~
%RO Version Control
vc
vc  ; Interface to external version control
    ;
    q
    ;
    ; Export all routines in ^ROUTINE to version control subdirectory
    ;
    ; TODO: delete routine, add new routine
    ;
export
    w "Exporting routines for version control...",!
    s dir=^Sys("data_dir")_$znspace_"\"
    s rnm=""
    f  s rnm=$o(^ROUTINE(rnm)) q:rnm=""  d
    . w rnm,!
    . d exportOne(dir,rnm)
    q
    ;
    ; export just one program
    ;
exportOne(dir,rname,fname="") n expFnm,io,dev,line,s
    i $e(dir,$l(dir))'="\" s dir=dir_"\"
    i fname="" s expFnm=dir_rname
    e  s expFnm=dir_fname
    s io=$io
    s dev="|FILE|"_expFnm o dev:("wtn"):1 e  u io d  q
    . w "Failed to use ",expFnm,!
    u dev
    w " ; ~Format=ANSI.S~",!
    w "%RO Version Control",!
    w rname,!
    ; write all routines line-by-line using ANSI format
    s line=0
    f  s line=$o(^ROUTINE(rname,line)) q:line=""  d
    . s s=^ROUTINE(rname,line)
    . i s="" s s=" "
    . w s,!
    w ! ; end of routine
    u io
    c dev
    q
    ;
    ; Import all routines from the version control subdirectory
    ;
importAll n dir,dev,fname
    w "Importing routines from version control...",!
    s dir=^Sys("data_dir")_$znspace_"\"
    ; get the list of files in the version control directory
    s dev="|PIPE|dir /b "_dir
    o dev:("rt")
    n $et="g endDir"
    u dev
    f  r fname d
    . d importOne(dir,fname)
endDir
    s $et=""
    c dev
    u $p
    q
    ;
    ; import a routine from version control
    ;
importOne(dir,fname) n fullPath,list
    i $e(dir,$l(dir))'="\" s dir=dir_"\"
    s fullPath=dir_fname
    s list=""
    d import^%RI(fullPath,list)
    q
    ;
    ; compare ^ROUTINE() code with code in version control subdirectory
    ; using winMerge or other comparison program
    ;
compare n rname,cfProg,dir,new,ret,file1,file2,cmd
    s dir=^Sys("temp_dir")
    w "Program name: "
    r rname w !
    i rname="" q
    i '$d(^ROUTINE(rname)) w "Routine does not exist!",! q
    s cfProg=^Sys("compare_program")
    ; export ^ROUTINE() to temporary file
    s file1=$j_"-"_rname
    d exportOne(dir,rname,file1)
    ; run external merge program, don't wait for it to finish,
    ; don't do any clean up later.
    ; TODO: Look at importing any merged changes somehow.
    s file1=dir_file1
    s file2=^Sys("data_dir")_$znspace_"\"_rname
    s cmd=cfProg_" "_file1_" "_file2
    s ret=$v("file",20,cmd,"s","")
    q
 

